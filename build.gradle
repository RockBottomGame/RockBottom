group 'de.ellpeck.rockbottom'
version '0.3.7'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
apply plugin: 'maven-publish'

mainClassName = "de.ellpeck.rockbottom.Main"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "de.ellpeck.rockbottom"
archivesBaseName = "RockBottom"

ext {
    proguardEnabled = new File(gradle.proguardDir).exists()
}

buildscript {
    ext {
        proguardEnabled = new File(gradle.proguardDir).exists()
    }

    repositories {
        jcenter()
        if (proguardEnabled)
            flatDir dirs: gradle.proguardDir
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.3'
        if (proguardEnabled)
            classpath ':proguard:'
    }
}

repositories {
    mavenCentral()

    maven {
        url "http://clojars.org/repo/"
    }
}

dependencies {
    compile project(":API")
    compile project(":Assets")

    //JOPT Simple for launch arg parsing
    compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '6.0-alpha-2'
}

jar {
    manifest {
        attributes("Main-Class": "de.ellpeck.rockbottom.Main")
    }
}

installDist {
    destinationDir = new File('build/temp/libs')
    exclude 'RockBottom-*'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
            artifact shadowJar
        }
    }
    repositories {
        maven { url "file://" + System.getenv("local_maven") }
    }
}

if (proguardEnabled) {
    task myProguardTask(type: proguard.gradle.ProGuardTask) {
        configuration 'settings.pro'

        injars 'build/libs'
        outjars 'build/obf/Obf.jar'
    }
    myProguardTask.dependsOn(installDist)

    task deleteFiles(type: Delete) {
        delete 'build/libs/'
        delete 'build/classes/'
    }
    deleteFiles.dependsOn myProguardTask

    shadowJar {
        from file("${buildDir}/obf")
        from file("${buildDir}/temp/libs/lib")
        exclude '*.sha1'
        exclude '*.git'
    }.dependsOn.clear()
    shadowJar.dependsOn(deleteFiles)

    task buildObf {

    }
    buildObf.dependsOn build
    buildObf.dependsOn installDist
}
