group 'de.ellpeck.rockbottom'
version '0.4.0'

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'application'
apply plugin: 'maven-publish'

mainClassName = "de.ellpeck.rockbottom.Main"

sourceCompatibility = 1.8
targetCompatibility = 1.8

group = "de.ellpeck.rockbottom"
archivesBaseName = "RockBottom"

if (System.getenv("BUILD_NUMBER") != null) {
    version = "${version}-${System.getenv("BUILD_NUMBER")}"
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

repositories {
    mavenCentral()

    maven {
        url "http://clojars.org/repo/"
    }
}

dependencies {
    compile project(":API")

    //JOPT Simple for launch arg parsing
    compile group: 'net.sf.jopt-simple', name: 'jopt-simple', version: '6.0-alpha-3'

    // OkHttps to create http connections for auth
    implementation "com.squareup.okhttp3:okhttp:4.9.0"
}

jar {
    manifest {
        attributes("Main-Class": "de.ellpeck.rockbottom.Main")
    }
}

installDist {
    destinationDir = new File('build/temp/libs')
    exclude 'RockBottom-*'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }
    repositories {
        maven { url "file://" + System.getenv("local_maven") }
    }
}
